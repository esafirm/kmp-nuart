//file:noinspection UnnecessaryQualifiedReference
plugins {
    id 'java-gradle-plugin'
    alias libs.plugins.kotlin.jvm
    alias libs.plugins.build.config
}

buildConfig {
    buildConfigField(String, 'APP_PLATFORM_GROUP', property('GROUP'))
}

java {
    sourceCompatibility = libs.versions.jvm.gradle.get()
    targetCompatibility = libs.versions.jvm.gradle.get()
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.fromTarget(libs.versions.jvm.gradle.get()))
    }
    explicitApi()
}

gradlePlugin {
    plugins {
        appPlugin {
            id = "nolambda.stream.nuart.app"
            displayName = "App Platform Gradle Plugin"
            implementationClass = "nolambda.stream.nuart.gradle.buildSrc.AppPlugin"
            description = "The Gradle convention plugin for app modules."
        }
        libPlugin {
            id = "nolambda.stream.nuart.lib"
            displayName = "App Platform Library Gradle Plugin"
            implementationClass = "nolambda.stream.nuart.gradle.buildSrc.LibraryPlugin"
            description = "The Gradle convention plugin for library modules."
        }
    }
}

dependencies {
    implementation libs.android.gradle.plugin.api
    implementation libs.android.gradle.plugin.asProvider()

    implementation libs.compose.gradle.plugin
    implementation libs.kotlin.gradle.plugin.api
    implementation libs.kotlin.multiplatform.gradle.plugin
    implementation libs.kotlin.compose.gradle.plugin
    implementation libs.kotlinx.binaryCompatibilityValidator
    runtimeOnly libs.kotlin.gradle.plugin.asProvider()

    // This is needed to reference KspExperimental for experimental features.
    compileOnly libs.ksp.api
    implementation libs.ksp.gradle.plugin

    implementation libs.graphviz.java
    implementation libs.kotlin.hierarchy.plugin
    implementation libs.ktfmt.gradle.plugin
    implementation libs.maven.publish.gradle.plugin
    implementation libs.detekt.gradle.plugin
}

tasks.register('release') {
    dependsOn('build', 'check', 'ktfmtCheck')
}
